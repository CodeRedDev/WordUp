// Top-level build file where you can add configuration options common to all sub-projects/modules.

buildscript {
    ext.versions = [
            "compileSdk"         : 29,
            "minSdk"             : 21,
            "targetSdk"          : 29,
            "sourceCompatibility": JavaVersion.VERSION_1_8,
            "targetCompatibility": JavaVersion.VERSION_1_8,
            "kotlin"             : "1.4.0",
            "kotlinJvmTarget"    : "1.8",
            "room"               : "2.2.5",
            "core"               : "1.3.1",
            "lifecycle"          : "2.2.0",
            "exoplayer"          : "2.11.8"
    ]
    ext.deps = [
            kotlinStdlibJdk8  : "org.jetbrains.kotlin:kotlin-stdlib-jdk8:${versions.kotlin}",
            androidxCore      : "androidx.core:core-ktx:${versions.core}",
            androidxLiveData  : "androidx.lifecycle:lifecycle-livedata-ktx:${versions.lifecycle}",
            androidxTestCore  : "androidx.test:core:1.3.0",
            androidxTestJunit : "androidx.test.ext:junit:1.1.2",
            androidxTestRunner: "androidx.test:runner:1.3.0",
            junit             : "junit:junit:4.13",
            roomRuntime       : "androidx.room:room-runtime:${versions.room}",
            exoplayerCore     : "com.google.android.exoplayer:exoplayer-core:${versions.exoplayer}"
    ]
    repositories {
        google()
        jcenter()
    }

    dependencies {
        classpath "com.android.tools.build:gradle:4.0.1"
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:${versions.kotlin}"
    }
}

plugins {
    id "org.jlleitschuh.gradle.ktlint" version "9.3.0" apply false
    id "org.jetbrains.dokka" version "1.4.0-rc" apply false
}

allprojects {
    repositories {
        google()
        jcenter()
    }
}

subprojects {
    apply plugin: "org.jlleitschuh.gradle.ktlint" // Version should be inherited from parent

    // Optionally configure plugin
    ktlint {
        debug = false
        verbose = false
        android = true
        outputToConsole = true
        reporters {
            reporter "plain"
            reporter "checkstyle"
        }
        ignoreFailures = false
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}
